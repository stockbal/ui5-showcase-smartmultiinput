@Metadata.layer: #CUSTOMER

@UI: {
  headerInfo: {
  typeName: 'Sales Order',
  typeNamePlural: 'Sales Orders',
  title: { type: #STANDARD, value: 'SalesOrder' }
  }
}

annotate view ZMIOBP_C_SalesOrderTP with
{
  @UI.facet: [ 
    {
      id: 'GeneralInfoFacetID',
      purpose: #STANDARD,
      position: 10,
      importance: #HIGH,
      label: 'General Information',
      type: #COLLECTION
    },
    {
      parentId: 'GeneralInfoFacetID',
      purpose: #STANDARD,
      position: 10,
      importance: #HIGH,
      label: 'Basic Data',
      type: #IDENTIFICATION_REFERENCE,
      targetQualifier: 'BasicData'
    },
    {
      parentId: 'GeneralInfoFacetID',
      purpose: #STANDARD,
      position: 20,
      importance: #HIGH,
      label: 'Additional Data',
      type: #FIELDGROUP_REFERENCE,
      targetQualifier: 'Additional'
    },
    { 
      id: 'SalesOrderItems',
      purpose: #STANDARD,
      type: #LINEITEM_REFERENCE,
      label: 'Items',
      position: 20,
      targetElement: '_SalesOrderItem' 
    }
  ] 
  @UI.hidden: true
  SalesOrderUUID;
  
  @UI: {
    lineItem: [ { position: 10, label: 'Sales Order ID', importance: #HIGH } ],
    selectionField: [ { position: 10 } ],
    identification:[ { position: 10, label: 'Sales Order ID' } ]
  }
  SalesOrder;
  
  @UI: {
    lineItem: [ { position: 20,label: 'Customer', importance: #MEDIUM } ],
    identification: [{ position: 20, label: 'Customer' }]
  }
  BusinessPartner;
  
  @UI: {
    lineItem: [ { position: 30, label: 'Status', importance: #MEDIUM } ],
    identification:[ { position: 30, label: 'Status' } ],
    selectionField: [ { position: 30} ]
  }
  OverallStatus;   
  
  @UI: { 
    identification: [{ label: 'Category', value: '_SlsOrdCategAssgmt.CategoryID', position: 40 }]
  }
  _SlsOrdCategAssgmt;
}